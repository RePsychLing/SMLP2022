---
title: "Anke Cajar: Detecting Faked IATs via Task-Switch Costs"
subtitle: "Model fitting"
author: "Douglas Bates"
date: "2022-09-05"
output: 
  html:
    toc: yes
    toc_depth: 3
    number_sections: yes
    embed-resources: true
callout-appearance: simple
jupyter: julia-1.8
---

# Setup

```{julia}
#| label: environment
#| echo: false
#| output: false
using Pkg; Pkg.activate(".")
```

# Fitting and examining models for the IAT data

Packages to be used

```{julia}
using Arrow           # to restore the data
using DataFrames      # data frame representation and manipulation
using MixedModels     # fit and examine mixed-effects models
using ProgressMeter   # for the progress indicator during model fitting

ProgressMeter.ijulia_behavior(:clear); # set behavior for Jupyter (IJulia)
```

## Read and trim the data

```{julia}
iat = DataFrame(Arrow.Table("./data/IAT_data.arrow"))
iat_trimmed = subset(iat, :rt => x -> 400 .≤ x .≤ 10_000)
describe(iat_trimmed)
```

::: {.callout-note collapse="true"}
### Why not use subset! to operate in-place

The way Arrow tables are read makes them read-only.
Instead of using `subset!` to operate in place, we create a new data frame of reduced size
:::

::: {.callout-note collapse="true"}
### Why write 10000 as 10_000?

Julia allows for (and ignores) underscores in numerical literals (i.e. numbers that are written out).
Writing `10_000` helps the human reader to parse the number, just as it would sometimes be written as `10,000` in North America or `10.000` in Europe.
:::

## Specifying contrasts

In R contrast specifications are stored with a factor but in Julia they are passed to the model-fitting functions as a separate `Dict` (dictionary) structure of key/value pairs.
The keys are `Symbol`s.
The simplest way to write a symbol is by prefacing its name with a colon, as in `:ID`.

The values in this dictionary can be contrast specifications from the `StatsModels` package or standardizing transformations from the `StandardizedPredictors` package.
The `MixedModels` package defines a special `Grouping` "contrast", which is not really a contrast at all but most of the others are technically not contrasts either so we ignore the inaccuracy in naming.
For models with many levels in one or more grouping factors (the name or expression behind the `|` in a random-effects term) it is important to specify the `Grouping` contrast.
It is not as important for grouping factors with fewer levels but always defining the grouping contrasts is a good habit to form.

We assign each of the two-level experimental factors the `EffectsCoding` contrast which results in a $\pm 1$ coding.
If it is important which of the levels corresponds to -1 and which to +1, use the optional argument `base` to declare the level that will be -1. 

:::{.callout-note collapse="true"}
### RK: Why it is almost always important to think about this ...

In (psychology) experiment the order of factor levels and the specification of `base` is usually important for the interpretation of results. There are two rules of thumb I recommend to consider:

1. Order levels according to inherent order (e.g., date, time, magnitude); this is needed for tables and figures.
2. Select base such that expected estimates of fixed effects are returned as positive values. This facilitates interpreation of interaction terms and correlation parameters. 
:::

The contrasts are special "types" in Julia - often what is called "a singleton type" meaning that there is only one object of that type and that single object is just a label.
The single value is created by, e.g. `Grouping()`.

```{julia}
#| lst-label: contrasts

contrasts = merge(
      Dict(:Group   => EffectsCoding(base= "Faking"; levels=["No_Faking", "Faking"])),
      Dict(:Time    => EffectsCoding(base= "Retest"; levels=["Baseline", "Retest"])),
      Dict(:Block   => EffectsCoding(base= "Incompatible"; levels=["Compatible", "Incompatible"])),
      Dict(:Switch  => EffectsCoding(base= "Yes"; levels=["No", "Yes"])),
      Dict(nm => Grouping() for nm in (:Subj, :Item))
   );
```

:::{.callout-note collapse="true"}
### RK: Difference between `EffectsCoding()` and R's `contr.sum()`

The default `base` in `EffectsCoding`() is the first level whereas for `contr.sum()` it is the last level. This must be taken into account for a compatible reproduction of the sign of effects across Julia and R. 
:::


## Model with scalar random effects

For comparison with the results from R, the models are fit to the response speed (`1/rt`), but 
I think a better scale would be the rate in Hz, evaluated as `1000 / rt`)

::: {.callout-note collapse="true"}
### RK: This recommendation was implemented

Dividing `1/rt` leads to tiny values if `rt` is recorded in milliseconds and estimates appearing as 0 in tables. Converting to Hz moves these estimates in a readable range.
:::

```{julia}
model12 = let
  f = @formula(1000/rt ~ 1 + Group * Time * Block * Switch + (1|Subj) + (1|Item))
  fit(MixedModel, f, iat_trimmed; contrasts)
end
```

```{julia}
VarCorr(model12)
```


```{julia}
model13 = let
  f = @formula(
    1000/rt ~ 1 + Group * Time * Block * Switch +
    (1 + Time+Switch+Block|Subj) + (1|Item)
  )
  fit(MixedModel, f, iat_trimmed; contrasts)
end
```

```{julia}
VarCorr(model13)
```

```{julia}
issingular(model13)
```

```{julia}
model13.PCA.Subj
```

```{julia}
versioninfo()
```
